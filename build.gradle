task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'

repositories {
    mavenCentral()
}

group = properties['project.group']
version = properties['project.version']

sourceCompatibility = properties['project.compatibility']
targetCompatibility = properties['project.compatibility']

dependencies {
    compile("org.apache.commons:commons-pool2:2.4.2")
    compile("org.slf4j:slf4j-api:1.7.21")

    testCompile("junit:junit:4.12")
    //for mocking
    testCompile("org.mockito:mockito-all:1.10.19")
    testRuntime("cglib:cglib-nodep:3.1")
    testRuntime("org.objenesis:objenesis:2.1")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

checkstyle {
    toolVersion = '6.7'
}

task checkstyleMainHtml << {
    ant.xslt(in: checkstyleMain.reports.xml.destination,
            style: file('./config/checkstyle/checkstyle.xsl'),
            out: new File("${buildDir}/reports/checkstyle", 'main.html'))
}

task checkstyleTestHtml << {
    ant.xslt(in: checkstyleTest.reports.xml.destination,
            style: file('./config/checkstyle/checkstyle.xsl'),
            out: new File("${buildDir}/reports/checkstyle", 'test.html'))
}
checkstyleMainHtml.dependsOn checkstyleMain
checkstyleTestHtml.dependsOn checkstyleTest
build.dependsOn jacocoTestReport, checkstyleMainHtml, checkstyleTestHtml
